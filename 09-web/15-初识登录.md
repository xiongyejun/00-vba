[http://club.excelhome.net/thread-1159783-24-1.html](http://club.excelhome.net/thread-1159783-24-1.html)

# 初识登录 #

登录其实没那么神秘。它也就是向服务器发送的一个POST请求。

我们来看一个简单的登录例子，以网易信箱（163.com）为例。

打开Fiddler，打开浏览器，登录网易信箱同时抓包。
搜索登录名vbatest（今天新注册的，你们可以随便整。哈哈）：

![](http://club.excelhome.net/data/attachment/forum/201410/28/114351l66gig6pkggswggj.png)

前面有![](http://club.excelhome.net/data/attachment/forum/201410/28/114348z1921o121dcy16do.png)标记的表明是POST请求

![](http://club.excelhome.net/data/attachment/forum/201410/28/114349yzri2ff4f2frtizv.png)

选择该session，点击Request框的Raw按钮通览一下所有发送内容：

![](http://club.excelhome.net/data/attachment/forum/201410/28/114349lxbz2jylj289a218.png)

password竟然是明码，小小鄙视下网易。


再看Response框。哇，满满的Set-Cookie啊。最关键的里面有一句“LOGIN_SUCCESS”！！

![](http://club.excelhome.net/data/attachment/forum/201410/28/114350kknk1wedyrndgwiw.png)

这样可以确定这个session是我们要找的登录session了！我们只要把它的Request的内容模拟好了就能成功登录（好期待）。

点击Request框的WebForms按钮通览发送的各参数：

![](http://club.excelhome.net/data/attachment/forum/201410/28/114350bauoza827r920drm.png)

居然没有动态参数。。（乐坏了）

cookie、referer的先不忙测试。先copy抓包内容，写代码：

	Sub Main()
	    Const username As String = "vbatest"
	    Const password As String = "12341234"
	    Dim strText As String
	    Dim uid As String
	    uid = username & "@163.com"
	    With CreateObject("MSXML2.XMLHTTP")
	        .Open "POST", "https://mail.163.com/entry/cgi/ntesdoor?df=mail163_letter&funcid=loginone&iframe=1&passtype=1&product=mail163&race=63_31_31_gz&uid=" & username & "@163.com", False
	        .setRequestHeader "Content-Type", "application/x-www-form-urlencoded"
	        .Send "savelogin=0&url2=http%3A%2F%2Fmail.163.com%2Ferrorpage%2Ferror163.htm&username=" & username & "&password=" & password & "&password="
	        strText = .getallresponseheaders
	        Debug.Print strText
	        strText = .responsetext
	        Debug.Print strText
	    End With
	End Sub

立即窗口：

![](http://club.excelhome.net/data/attachment/forum/201410/28/114901imi6mtiyyay6bmoa.png)

登录成功！


小贴士：
1、不要以密码作为搜索内容。密码经常被加密。以密码原文作为搜索内容很可能搜不到所需session。
2、以登录名作为搜索内容相对好些。但也有可能被加密而搜不到正确的session。这时搜到的包含登录名的最早的session可能是登录后出现的页面。登录的session前面一般都有post的图标，可以根据这个再加上查看Request和Response的内容判断是否是我们所需的登录session。


----------
经过kangatang提醒，本例经过测试，将SendData的参数作为URL的参数用GET请求也可以实现登录，代码如下（原SendData的参数我精简了，只留了用户名和密码）：

	Sub Main()
	    Const username As String = "vbatest"
	    Const password As String = "12341234"
	    
	    With CreateObject("MSXML2.XMLHTTP") 'CreateObject("WinHttp.WinHttpRequest.5.1") '
	        .Open "GET", "https://mail.163.com/entry/cgi/ntesdoor?df=mail163_letter&funcid=loginone&iframe=1&passtype=1&product=mail163&race=63_31_31_gz&uid=" & username & "@163.com&" & "username=" & username & "&password=" & password, False
	        .send
	        Debug.Print .Getallresponseheaders
	        Debug.Print .responsetext
	    End With
	End Sub

但不是所有的网站都可以这样的。这只是个例。

# 登录之后可以做什么----查询数据 #

登录并非是我们的最终目的。最终目的是查询一些非登录不能查看的数据，或是发送数据。如论坛登录后，可下载附件，可发帖；邮箱登录后，可收件发件。


前面讲过，xmlhttp和winhttp只要该对象不销毁，都可以保持cookie。我们登录就是为了取得一个被允许查看数据和发送数据的cookie，然后执行查询和发送的请求。

仍以网易邮箱为例：

登录后网页显示邮件列表，此过程抓包。

![](http://files.c.excelhome.net/forum/201410/28/1640086gd8d6jjijgdgdfh.png)

抓包后，在fiddler内搜索邮件列表页面内的某字符串，查找到显示邮件列表的页面：（Response返回一串JSON数据）

![](http://files.c.excelhome.net/forum/201410/28/164009mwtiz3moswhim79k.png)

拷贝Request的相关内容，编写代码：

	Sub Main()
	    Const username As String = "vbatest"
	    Const password As String = "12341234"
	    Dim strText As String
	    Dim sid As String
	    
	    With CreateObject("MSXML2.XMLHTTP") 'CreateObject("WinHttp.WinHttpRequest.5.1") '
	        .Open "POST", "https://mail.163.com/entry/cgi/ntesdoor?df=mail163_letter&funcid=loginone&iframe=1&passtype=1&product=mail163&race=63_31_31_gz&uid=" & username & "@163.com", False
	        .setrequestheader "Content-Type", "application/x-www-form-urlencoded"
	        .send "username=" & username & "&password=" & password
	        sid = Split(Split(.responsetext, "sid=")(1), "&")(0)
	        
	        .Open "POST", "http://mail.163.com/js6/s?sid=" & sid & "&func=mbox:listMessages&topNav_mobileIcon_show=1&TopTabReaderShow=1&TopTabReaderTipsShow=1&FrameMasterMailPopupClose=1&welcome_welcomemodule_mailrecom_click=1&LeftNavfolder1Click=1&mbox_folder_enter=1", False
	        .setrequestheader "Content-Type", "application/x-www-form-urlencoded"
	        .setrequestheader "Accept", "text/javascript" '不加这句的话返回的不是json是xml数据
	        .send "var=" & encodeURI("<?xml version=""1.0""?>" _
	            & "<object>" _
	            & "<int name=""fid"">1</int>" _
	            & "<string name=""order"">date</string>" _
	            & "<boolean name=""desc"">true</boolean>" _
	            & "<int name=""limit"">20</int>" _
	            & "<int name=""start"">0</int>" _
	            & "<boolean name=""skipLockedFolders"">false</boolean>" _
	            & "<string name=""topFlag"">top</string>" _
	            & "<boolean name=""returnTag"">true</boolean>" _
	            & "<boolean name=""returnTotal"">true</boolean>" _
	            & "</object>")        
			
			Debug.Print .responsetext
	    End With
	End Sub

查看邮件列表的SendData是一个xml数据。不管它什么数据，我们都照样copy就是了。

# 登录之后可以做什么----发送数据 #

上一贴说的是登录后查看邮件。这次来试一次发送邮件。


手工登录、发送邮件，同时抓包。


截取发送邮件内容的部分字符串作为搜索文本，找到session。


拷贝Request里相关内容，编写代码：

	Sub Main()
	    Const Username As String = "vbatest"
	    Const Password As String = "12341234"
	    Const Account As String = "vbatest@163.com"
	    Const ToAccount As String = "wcymiss@163.com"
	    Const Subject As String = "主题：用web发送邮件"
	    Const Content As String = "正文：看到此邮件则证明发送成功"
	    Dim strText As String
	    Dim Sid As String
	    Dim Senddata
	    
	    With CreateObject("MSXML2.XMLHTTP")
	        .Open "POST", "https://mail.163.com/entry/cgi/ntesdoor?df=mail163_letter&funcid=loginone&iframe=1&passtype=1&product=mail163&race=63_31_31_gz&uid=" & Username & "@163.com", False
	        .setrequestheader "Content-Type", "application/x-www-form-urlencoded"
	        .Send "username=" & Username & "&password=" & Password
	        Sid = Split(Split(.responsetext, "sid=")(1), "&")(0)
	        
	        .Open "POST", "http://mail.163.com/js6/s?sid=" & Sid & "&func=mbox:compose&FrameMasterMailPopupClose=1&cl_send=2&l=compose&action=deliver", False
	        .setrequestheader "Content-Type", "application/x-www-form-urlencoded"
	        .setrequestheader "Accept", "text/javascript" '不加这句的话返回的不是json是xml数据
	        Senddata = "var=" & encodeURI("<?xml version=""1.0""?>" _
	            & "<object>" _
	            & "<string name=""id"">c:" & GetLongTime() & "</string>" _
	            & "<object name=""attrs"">" _
	            & "<string name=""account"">" & Account & "</string>" _
	            & "<boolean name=""showOneRcpt"">false</boolean>" _
	            & "<array name=""to""><string>" & ToAccount & "</string></array>" _
	            & "<array name=""cc""/><array name=""bcc""/>" _
	            & "<string name=""subject"">" & Subject & "</string>" _
	            & "<boolean name=""isHtml"">true</boolean>" _
	            & "<string name=""content"">" & Content & "</string>" _
	            & "<int name=""priority"">3</int>" _
	            & "<boolean name=""saveSentCopy"">true</boolean>" _
	            & "<string name=""charset"">GBK</string></object>" _
	            & "<boolean name=""returnInfo"">false</boolean>" _
	            & "<string name=""action"">deliver</string>" _
	            & "<int name=""saveTextThreshold"">1048576</int>" _
	            & "</object>")
	        .Send Senddata
	        Debug.Print .responsetext
	    End With
	End Sub
	Function encodeURI(strTobecoded As String) As String
	    With CreateObject("msscriptcontrol.scriptcontrol")
	        .Language = "JavaScript"
	        encodeURI = .eval("encodeURIComponent('" & strTobecoded & "');")
	    End With
	End Function
	
	Function GetLongTime()
	    With CreateObject("msscriptcontrol.scriptcontrol")
	        .Language = "JavaScript"
	        GetLongTime = .eval("new Date().getTime();")
	    End With
	End Function

SendData里有部分参数还可以精简。有兴趣的话可以自己测试。

GetLongTime自定义函数的作用是产生一个国际通用的表示当前时间的13位数字。

像网易这么简单的登录毕竟少见。


很多网站的登录都会发送很多参数。参数是明码的、可以通过抓包搜索得到的，登录方法和第二篇章的获取数据的方法相同，这里就不再举例了。


下面将举一些比较复杂的、抓包后搜索不到明码参数的登录的例子。

复杂的登录需要学习Javascript知识。


Javascript知识入门请到这里：http://www.w3school.com.cn/js/index.asp

javascript知识深入请参阅此书：JavaScript权威指南（第6版）。下载地址：http://pan.baidu.com/s/1dDeOgoh

