[http://club.excelhome.net/forum.php?mod=viewthread&tid=1222948&extra=page%3D1](http://club.excelhome.net/forum.php?mod=viewthread&tid=1222948&extra=page%3D1)

	
	Sub 识别图片()
	    Columns("A:A").NumberFormatLocal = "@"
	    t = Timer
	    Dim arr() As Byte
	    Dim b(1 To 67, 1 To 2)
	    Dim a(1 To 4)
	    
	    For P = 1 To 67
	        arr = ""
	        Open ThisWorkbook.Path & "\" & P & ".bmp" For Binary As #1
	        ReDim arr(1 To LOF(1))    '将数据保存到字节
	        Get #1, , arr
	        Close #1
	        
	        For i = 1 To 4
	            a(i) = 0
	            If arr((5 * 40 + 11 + (i - 1) * 8) * 3 + 54) < 200 Then a(i) = 1: GoTo 1
	            If arr((7 * 40 + 7 + (i - 1) * 8) * 3 + 54) < 200 Then a(i) = 2: GoTo 1
	            If arr((4 * 40 + 6 + (i - 1) * 8) * 3 + 54) < 200 Then a(i) = 3: GoTo 1
	            If arr((8 * 40 + 7 + (i - 1) * 8) * 3 + 54) < 200 Then a(i) = 4: GoTo 1
	            If arr((6 * 40 + 9 + (i - 1) * 8) * 3 + 54) < 200 Then a(i) = 5: GoTo 1
	            If arr((10 * 40 + 8 + (i - 1) * 8) * 3 + 54) < 200 And arr((10 * 40 + 9 + (i - 1) * 8) * 3 + 54) < 200 Then a(i) = 6: GoTo 1
	            If arr((7 * 40 + 8 + (i - 1) * 8) * 3 + 54) < 200 Then a(i) = 7: GoTo 1
	            If arr((6 * 40 + 11 + (i - 1) * 8) * 3 + 54) < 200 Then a(i) = 8: GoTo 1
	            If arr((9 * 40 + 10 + (i - 1) * 8) * 3 + 54) < 200 And arr((9 * 40 + 11 + (i - 1) * 8) * 3 + 54) < 200 Then a(i) = 9
	1:
	        Next i
	        
	        b(P, 1) = a(1) & a(2) & a(3) & a(4)
	    Next P
	    
	    Cells(7, 5) = "识别时间" & Timer - t & "秒"
	    [a1:b67] = b
	End Sub

[http://club.excelhome.net/forum.php?mod=viewthread&tid=365263&extra=page%3D1%26filter%3Dtypeid%26typeid%3D110&page=1](http://club.excelhome.net/forum.php?mod=viewthread&tid=365263&extra=page%3D1%26filter%3Dtypeid%26typeid%3D110&page=1)

# 简单验证码识别 #

已知条件：

目录下的bmp文件夹中有300个 BMP 图像（24位位图）

目录下有“模板.bmp” BMP 图像（24位位图）

bmp目录下的图片均为 40像素宽 10像素高，大小均为1254 字节

分别显示4位数字，所有图片背景色一样。

虽然图片为彩色，但所有单个数字的颜色一样（例：图片中所有的“1” 都是同样的颜色）

模板.bmp 由出题者制作，100像素宽 10像素高，大小为3055字节

显示 0 到 9， 10位数字

为便于核对，bmp目录下所有图片均以所示数字为名。
　
## 题目1 ##
判断 2分

请根据“模板.bmp” 依次读取300个图片中所示数字填写在 B列，文件名填写在 A列

注意：不是从文件名判断所示数字，文件名仅是便于验证结果、制作模板时用
　
## 题目2 ##
模板制作 2 分

编写代码从 300 个图片中提取若干（自选）图片生成模板。

注意：模板制作需要人工判断，允许使用文件名中的数字

模板格式可以是任何形式，如所制作模板与"模板.bmp" 不同，

需要编写 相应的代码依次读取 300个图片（题目1 可以此代替）。

首先要大致了解，BMP24位位图的储存方式（Zldccmx 的答案中有比较详细的比对、介绍）

图像最小单位是像素，通常说的屏幕分辨率 800 * 600 就是 宽800像素高600像素
每个图片查看器属性，也都可以看到长宽的像素值。

每个像素都以 红蓝绿三色来表示，每个颜色都是以0-255 来表示（BYTE）

但BMP文件的存储顺序为绿蓝红（反的），但这里只是进行比较判断的话，可以不用理会

象bmp文件夹中的图片 都是40 * 10像素，那么它的大小应该是40 * 10 * 3 =1200 字节才对

但实际大小是1254字节，多出来的54个字节就是文件头，通过查看属性得到的信息都在这54个字节里面

这种图片格式 可以说是“原生态” 格式。其他类型的文件格式如JPG PNG等都可以优化压缩bmp文件

但是不管哪种文件格式，只要他的像素乘积相同，那么它的显示的时候所消耗的显存也是相同的。

要验证这个说法也很容易，找一个比较大的图片，分别另存为 24位BMP 和jpeg格式，文件大小的区别约10倍

用两个image控件分别导入这两个图片。

然后用SavePicture方法分别导出，所导出的两个文件大小一模一样

关于图片的文件结构，请查阅相关资料，这里不多废话了

了解了图片文件结构，就可以用二进制数组跳过文件头直接取得图像数据，来比对分析

二进制数组是一条状的（线性的），而显示出来图片是平面的。电脑显示图片的时候,
是按照文件头的 宽、高 属性逐行逐点显示的（如果用程序把宽高换个位，那么显示出来的就是凌乱的图像）。

有一点跟人们的思维模式不同的是，人的思维习惯是从左到右、从上倒下来摆顺序的，但BMP文件不是这样的。它是从左到右、从下到上来摆顺序的。

![](http://files.c.excelhome.net/forum/month_0811/20081129_9eafe4897ec8823233afXP5WPQa8Rood.png)

在这个4个数字的图片中每个数占1/4 (10*10像素）就是300字节 
我们可以像电脑显示图片那样循环，用MOD \ 等一些算法， 
取得每个数的300字节，分别与模板中的比较，从而得到图片所示数字。
模板的制作，简单的还是数字做成10个文件，二进制数组写成文件，分别命名为0 到 9 
或者合并成一个文件，以数组套数组的形式写入文件

ldy代码：
	
	Sub getBMP1()
	    Dim Bit(299) As Byte, sNum As String, drs As String
	    Dim Bt4(1199) As Byte
	    Dim BtArrX4(3), BtArrX10(9)
	    Dim t As Double
	    Dim i As Long, j As Long, irow As Long
	    
	    t = Timer
	    If Dir(ThisWorkbook.Path & "\0-9.自制模板") = "" Then MsgBox "请先制作模板": Exit Sub
	    Range("a:b").Value = Empty
	    For i = 0 To 3    'Dim BtArrX4(3)as byte(299) 语法不支持这样写，但可以用自定义类型，dim BtArrX4(3)as btt
	        BtArrX4(i) = Bit    '这里用循环赋值，达到类似效果
	    Next
	
	    Open ThisWorkbook.Path & "\0-9.自制模板" For Binary As #1
	    Get #1, , BtArrX10
	    Close #1
	
	    drs = Dir(ThisWorkbook.Path & "\bmp\*.bmp")
	    Do
	        Open ThisWorkbook.Path & "\bmp\" & drs For Binary As #1
	        Seek #1, 55
	        Get #1, , Bt4
	        Close #1
	
	        For i = 0 To 1199
	            BtArrX4(i \ 30 Mod 4)((i \ 120) * 30 + (i Mod 30)) = Bt4(i)
	        Next
	        sNum = "" '需要的结果
	        
	        For i = 0 To 3    '进行比对
	            For j = 0 To 9
	                If StrComp(BtArrX4(i), BtArrX10(j)) = 0 Then sNum = sNum & j: Exit For
	            Next
	        Next
	        irow = irow + 1
	        Cells(irow, 2) = sNum
	        Cells(irow, 1) = drs
	        drs = Dir    '下一个文件名
	    Loop While drs <> ""
	    Range("c1").Value = Timer - t
	End Sub
	Sub getBMP()
	    Dim t As Double
	    Dim i As Long, j As Long, irow As Long
	    Dim x As Long, y As Long
	    
	    Dim Bit(299) As Byte, sNum As String, drs As String
	    Dim Bt4(1199) As Byte, Bt10(2999) As Byte
	    Dim BtArrX4(3), BtArrX10(9)
	    t = Timer
	    Range("a:b") = Empty
	    
	    For i = 0 To 3
	        BtArrX4(i) = Bit
	    Next
	    For i = 0 To 9
	        BtArrX10(i) = Bit
	    Next
	
	
	'取得样板
	    Open ThisWorkbook.Path & "\模板.bmp" For Binary As #1
	    Seek #1, 55 '前面54字节都是文件头，
	    Get #1, , Bt10
	    Close #1
	    For i = 0 To 2999 ' 逐行拆分为10 组数据
	        x = i \ 30 Mod 10
	        y = (i \ 300) * 30 + i Mod 30
	        BtArrX10(x)(y) = Bt10(i)
	    Next
	''取得样板
	
	'读取 bmp文件夹
	    drs = Dir(ThisWorkbook.Path & "\bmp\*.bmp")
	    Do
	        
	        Open ThisWorkbook.Path & "\bmp\" & drs For Binary As #1
	        Seek #1, 55
	        Get #1, , Bt4 ' 一维数组，线状的
	        Close #1
	
	        For i = 0 To 1199 ' 一维数组变"二维"
	            BtArrX4(i \ 30 Mod 4)((i \ 120) * 30 + (i Mod 30)) = Bt4(i)
	        Next
	        
	        sNum = ""
	        For i = 0 To 3 '进行比对
	            For j = 0 To 9
	                If StrComp(BtArrX4(i), BtArrX10(j)) = 0 Then sNum = sNum & j: Exit For
	            Next
	        Next
	        
	        irow = irow + 1
	        Cells(irow, 2) = sNum
	        Cells(irow, 1) = drs
	        drs = Dir '下一个文件名
	    Loop While drs <> ""
	    Range("c2").Value = Timer - t
	End Sub
	
	Sub MkFile()
	    Dim Bit(299) As Byte, drs As String
	    Dim Bt4(1199) As Byte, Bt10(2999) As Byte
	    Dim BtArrX4(3), BtArrX10(9)
	    Dim x As Byte, sum As Byte
	    Dim i As Long, j As Long, ii As Long
	    Dim dd As Long
	    Dim newbt() As Byte
	    
	    For i = 0 To 3
	        BtArrX4(i) = Bit
	    Next
	
	    'bmp文件夹 有300个文件，样板是足够的，并且文件名与图片显示数字一致，不用专门挑了
	    '如果文件名与图片不一致，先人工判断，挑选足够的样板，放到另外一个文件夹，文件名改成与图片一致
	    drs = Dir(ThisWorkbook.Path & "\bmp\*.bmp")
	    Do
	        dd = dd + 1
	        If sum = 10 Then Exit Do    ' 说明0--9 10个样板 已经了退出循环
	
	        Open ThisWorkbook.Path & "\bmp\" & drs For Binary As #1
	        Seek #1, 55
	        Get #1, , Bt4
	        Close #1
	
	        For i = 0 To 1199
	            BtArrX4(i \ 30 Mod 4)((i \ 120) * 30 + (i Mod 30)) = Bt4(i)
	        Next
	
	        For j = 1 To 4
	            x = Val(VBA.Mid(drs, j, 1))    '5231.bmp
	            If IsEmpty(BtArrX10(x)) Then    '如果是空的就填一个数组进去
	                BtArrX10(x) = BtArrX4(j - 1)
	                sum = sum + 1       '计数，有10个就够了
	            End If
	        Next
	        drs = Dir
	    Loop While drs <> ""
	
	    Open ThisWorkbook.Path & "\0-9.自制模板" For Binary As #1
	    Put #1, , BtArrX10
	    Close #1
	
	    '''''''''''''''''制作图片第二种模板'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
	    drs = Dir(ThisWorkbook.Path & "\bmp\*.bmp")    '制作图片模板,随便从300个里面找一个文件
	    Open ThisWorkbook.Path & "\bmp\" & drs For Binary As #1
	    ReDim newbt(LOF(1) - 1) As Byte
	    Get #1, , newbt
	    Close #1
	
	    Open ThisWorkbook.Path & "\新模板.bmp" For Binary As #1
	    Put #1, , newbt    '先写到硬盘，接着再改，如果此时结束代码相当于复制 文件
	'    Seek #1, 3 '跳至 3位 3到6共4个字节，Long值，表示宽度
	    Put #1, 3, 3054& '改写文件大小
	'    Seek #1, 19    '跳至 19位 19到23共4个字节，Long值，表示宽度
	    Put #1, 19, 100&    ' 10像素 改为100 像素宽
	    Seek #1, 55    '跳至 55 开始写图像数据BtArrX10()
	    For i = 0 To 299 Step 30
	        For j = 0 To 9
	            For ii = 0 To 29
	                Put #1, , CByte(BtArrX10(j)(ii + i))
	            Next ii
	        Next j
	    Next i
	    Close #1
	    MsgBox "已完成两种模板 0-9.自制模板 和 新模板.bmp "
	End Sub


Zldccmx

	Sub 原始模板识别()
	    Call 识别("模板.bmp")
	    Call 识别率判断
	End Sub
	Sub 模板验证()
	    Call 识别("Zldccmx模板.bmp")
	    Call 识别率判断
	End Sub
	Sub 识别率判断()
	    Arr = Sheet1.[A1].Resize(300, 2)
	    K = 0
	    For I = 1 To 300
	        If Left(Arr(I, 1), 4) <> "" & Arr(I, 2) Then K = K + 1
	    Next
	    MsgBox "经系统检查，识别误差率为：" & Format(K / 300, "0.00%") & vbLf & vbLf & "      准确率为:" & Format((300 - K) / 300, "0.00%")
	    Erase Arr
	End Sub

识别代码：
	
	'*****************************************************************************
	'由24位BMP文件存储结构知：前2字节为固定值，也就是BMP文件的特征值；
	'3-6字节为文件大小信息；
	'7-10字节为保留字段
	'11-14字节为位图数据开始之前的偏移量（单位：byte）
	'前14字节为BMP文件的文件头
	'*****************************************************************************
	'15-18字节记录下 Bitmap Info Header 的长度
	'19-22字节记录下 位图的宽度，以像素（pixel）为单位
	'23-26字节记录下 位图的高度，以像素（pixel）为单位
	'27-28字节记录下 位图的位图层数
	'29-30字节记录下 每个像素的位数
	'31-34字节记录下 压缩方式
	'35-38字节记录下 位图数据的大小（单位：byte）
	'39-42字节记录下 水平分辨率（单位：像素/公尺）
	'43-46字节记录下 垂直分辨率（单位：像素/公尺）
	'47-50字节记录下 位图使用的调色盘颜色数
	'51-54字节记录下 重要的颜色数
	'*****************************************************************************
	'从55字节开始，才是BMP图像各点阵的记录，每个像素点3个字节，分别记录RGB颜色信息
	'*****************************************************************************
	'本代码就是根据以上信息来识别
	Sub 识别(Fname$)
	    Dim N&, I%, J%, K%, Mm%, M%, Drr(), Flag As Boolean
	    Dim Arr() As Byte, Pa$, Fn, Dic
	    Application.ScreenUpdating = False    '关闭屏幕更新
	    Pa = ThisWorkbook.Path & "\"    '设置路径
	'    Fnamne = "Zldccmx模板.bmp" '"模板.bmp"              '设置文件名
	    Open Pa & Fname For Binary As 1    '以二进制打开图像文件
	    N = LOF(1)                   '文件的字节数
	    ReDim Arr(1 To N) As Byte    '定义一个一维数组，记录下当前文件的信息
	    Get 1, , Arr                 '一次性将文件信息存入数组ARR中
	    Close 1                      '关闭已经打开的图像文件
	    '定义一个数组，记录模板中的全部信息
	    ReDim Drr(1 To 10, 1 To (3 * 10 * 10))  '0-9共10 个数,由于每个数字占10*10像素，而每像素占3个字节，所以*3
	    For M = 1 To 10    '将模板中的0123456789拆分成0，1，2，3，4，5，6，7，8，9，分别记录在数组中，将1维数组拆分成多维数组，每一行对应一个数字
	        K = 0    '这是由位图记录格式决定的，从图像左下角第一个像素开始往右，依次记录至右下角最后一个像素，然后按反Z字型逐行往上推进倒数第二行最左至最右....，最后记录的是图像右上角像素的信息
	        For I = 1 To Int((N - 54) / 4) * 4 Step (3 * Arr(19))    'Arr(19)为图像宽度
	            K = K + 1
	            For J = 1 To 3 * 10
	                Drr(M, (K - 1) * 3 * 10 + J) = Arr(54 + I + J - 1 + (M - 1) * 3 * 10)    'BMP文件从54字节开始存储图像内容
	            Next
	        Next
	    Next
	    '数组转换成功
	    Set Dic = CreateObject("Scripting.Dictionary")    '创建一个字典，记录各文件名
	    Pa = ThisWorkbook.Path & "\bmp\"               '设置路径
	    Fn = Dir(Pa & "*.BMP")                         '查找指定目录下的所有BMP文件
	    Do While Fn <> ""
	        Dic(Fn) = ""                               '将查找到的文件目录记录在字典中
	        Fn = Dir                                   ' 查找下一个文件
	    Loop
	    Sheet1.[A:B] = ""                              '将工作表的A：B列清空
	    For Each Fn In Dic.Keys                 '读取文件名
	        Open Pa & Fn For Binary As 1    '以二进制打开图像文件
	        N = LOF(1)    '文件的字节数
	        ReDim Arr(1 To N) As Byte    '定义一个一维数组，记录下当前文件的信息
	        Get 1, , Arr    '一次性将文件信息存入数组ARR中
	        Close 1    '关闭已经打开的图像文件
	        For M = 1 To 4    '已知每一个图像包含4个字符
	            ReDim Crr(1 To 3 * 10 * 10)
	            K = 0
	            For I = 1 To Int((N - 54) / 4) * 4 Step (3 * Arr(19))
	                K = K + 1
	                For J = 1 To 3 * 10
	                    Crr((K - 1) * 3 * 10 + J) = Arr(54 + I + J - 1 + (M - 1) * 3 * 10)    'BMP文件从54字节开始存储图像内容
	                Next
	            Next
	            For Mm = 0 To 9  '将当前的一组数据与 模板 中 0 至 9 逐组进行比较
	                For J = 1 To 300
	                    If Crr(J) <> Drr(Mm + 1, J) Then
	                        Flag = False '如果不相同则将标志置为 假
	                        Exit For '退出本层(J)循环，找下一组 Mm
	                    Else
	                        Flag = True '标志置为真
	                    End If
	                Next J
	                If Flag = True Then Dic(Fn) = Dic(Fn) & Mm: Flag = False: Exit For    '找到了完全一组的数据就记录下当前的组号，记录在字典的ITEM中
	            Next Mm
	        Next M
	    Next Fn
	    Sheet1.[A1].Resize(Dic.Count) = WorksheetFunction.Transpose(Dic.Keys)    '将图像名存放在A列
	    Sheet1.[B1].Resize(Dic.Count) = WorksheetFunction.Transpose(Dic.Items)    '将从图像中识别出来的字符存放在B列
	    '结束识别任务
	End Sub
	
制作模板：
	
	Sub 制作模板()
	    Dim Flag As Boolean
	    Dim Arr() As Byte, Zrr() As Byte
	    Set Dic = CreateObject("Scripting.Dictionary")    '创建一个字典，记录各文件名
	    Pa = ThisWorkbook.Path & "\bmp\"               '设置路径
	    Fn = Dir(Pa & "*.BMP")                         '查找指定目录下的所有BMP文件
	    Do While Fn <> ""
	        Dic(Fn) = ""                               '将查找到的文件目录记录在字典中
	        Fn = Dir                                   ' 查找下一个文件
	    Loop
	    ReDim Zrr(1 To 3054) '每个字符点10*10点阵，第个点阵由3字节组成，共有10个字符,文件体为3*10*10*10=3000字节，加上文件头54字节，总共3054字节
	    Set DiD = CreateObject("Scripting.Dictionary") '引用一个字典，记录0-9，为后面的循环加快速度
	    For I = 0 To 9: DiD("" & I) = "": Next
	    ReDim Crr(1 To 10, 1 To 3 * 10 * 10)
	    For Each ke In DiD.Keys
	        If DiD.Exists(ke) Then '如果字典中包括某个值就执行，
	        '或许你会纳闷，在遍历字典的时候不是每个KEY均包含在字典中吗？
	        '==,在 标号 100语句处，对已经处理过的数字就从字典中移除!
	        '但由于循环初始化在先，移除在后，所以移除字典中的KEY对循环是不受影响的。要想左右这个循环，只有加上 上面这一句判断了!
	            Fn = Filter(Dic.Keys, "" & ke)(0)
	            Open Pa & Fn For Binary As 1    '以二进制打开图像文件
	            N = LOF(1)    '文件的字节数
	            ReDim Arr(1 To N) As Byte    '定义一个一维数组，记录下当前文件的信息
	            Get 1, , Arr    '一次性将文件信息存入数组ARR中
	            Close 1    '关闭已经打开的图像文件
	            If Not Flag Then    '生成一个文件头
	                For I = 1 To 54: Zrr(I) = Arr(I): Next
	                Zrr(19) = 100: Zrr(23) = 10
	                fsize = Hex(3000 + 54)    '将文件大小转化成16进制
	                If Len(fsize) = 3 Then fsize = "0" & fsize    '强制转换成4位,由于文件尺寸较小，所以没有考虑 超过4位的情况
	                Dim v(1 To 4)
	                For A = 1 To 4 '将16进制的文件大小用2个字节来记录
	                    Select Case Mid(fsize, A, 1)
	                    Case "A"
	                        v(A) = 10
	                    Case "B"
	                        v(A) = 11
	                    Case "C"
	                        v(A) = 12
	                    Case "D"
	                        v(A) = 13
	                    Case "E"
	                        v(A) = 14
	                    Case "F"
	                        v(A) = 15
	                    Case Else
	                        v(A) = Mid(fsize, A, 1)
	                    End Select
	                Next
	                Zrr(3) = v(3) * 16 + v(4)    '低位在前
	                Zrr(4) = v(1) * 16 + v(2)    '高位在后
	                Flag = True    '通知系统，文件头已经生成，不必再重复生成
	            End If
	            Fn = Split(Fn, ".")(0)
	            For M = 1 To Len(Fn)
	                Z = Mid(Fn, M, 1)
	                If DiD.Exists("" & Z) Then
	                    K = 0
	                    For I = 1 To Int((N - 54) / 4) * 4 Step (3 * Arr(19))
	                        K = K + 1
	                        For J = 1 To 3 * 10
	                            Crr(Z + 1, (K - 1) * 3 * 10 + J) = Arr(54 + I + J - 1 + (M - 1) * 3 * 10)    'BMP文件从54字节开始存储图像内容
	                        Next J
	                    Next I
	100:                    DiD.Remove ("" & Z) '已经完成的就将该值从字典中移除!
	                End If
	            Next M
	        End If
	    Next
	    For M = 1 To 10    '数组转换，合并成模板
	        K = 0
	        For I = 1 To 3000 Step 300    '每个字符点10*10点阵，第个点阵由3字节组成，共有10个字符
	            K = K + 1
	            For J = 1 To 3 * 10
	                Zrr(54 + I + J - 1 + (M - 1) * 3 * 10) = Crr(M, (K - 1) * 3 * 10 + J)    'BMP文件从54字节开始存储图像内容
	            Next
	        Next
	    Next
	    Fn = "Zldccmx模板.bmp"
	    Pa = ThisWorkbook.Path & "\"
	    Open Pa & Fn For Binary As 1    '以二进制打开图像文件
	    ' N = LOF(1)    '文件的字节数
	    ReDim Arr(1 To N) As Byte    '定义一个一维数组，记录下当前文件的信息
	    Put 1, , Zrr    '一次性将文件信息存入数组ARR中
	    Close 1
	    MsgBox "新的模板生成成功!模板保存在 " & Pa & "目录下，文件名为：" & Fn & vbLf & vbLf & "  现在开始验证......"
	    Call 模板验证
	End Sub
	
