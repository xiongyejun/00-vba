[http://club.excelhome.net/forum.php?mod=viewthread&tid=817480&extra=page%3D1](http://club.excelhome.net/forum.php?mod=viewthread&tid=817480&extra=page%3D1)

模块hBmpSaveToFile

	Private Declare Function GdiplusStartup Lib "GDIPlus" (token As Long, inputbuf As GdiplusStartupInput, ByVal outputbuf As Long) As Long
	Private Declare Function GdiplusShutdown Lib "GDIPlus" (ByVal token As Long) As Long
	Private Declare Function GdipDisposeImage Lib "GDIPlus" (ByVal Image As Long) As Long
	Private Declare Function GdipSaveImageToFile Lib "GDIPlus" (ByVal Image As Long, ByVal filename As Long, clsidEncoder As GUID, encoderParams As Any) As Long
	Private Declare Function CLSIDFromString Lib "ole32" (ByVal str As Long, id As GUID) As Long
	Private Declare Function GdipCreateBitmapFromHBITMAP Lib "GDIPlus" (ByVal hbm As Long, ByVal hPal As Long, BITMAP As Long) As Long
	Private Type GUID
	    Data1   As Long
	    Data2   As Integer
	    Data3   As Integer
	    Data4(7)   As Byte
	End Type
	
	Private Type GdiplusStartupInput
	 GdiplusVersion As Long
	 DebugEventCallback As Long
	 SuppressBackgroundThread As Long
	 SuppressExternalCodecs As Long
	End Type
	
	Private Type EncoderParameter
	 GUID As GUID
	 NumberOfValues As Long
	 type As Long
	 Value As Long
	End Type
	
	Private Type EncoderParameters
	 Count As Long
	 Parameter As EncoderParameter
	End Type
	 
	Public Function SavehBitmapToJPGFile(ByVal hBmp As Long, ByVal sFileName As String, Optional ByVal quality As Byte = 80) As Integer
	    Dim tSI As GdiplusStartupInput
	    Dim lRes As Long
	    Dim lGDIP As Long
	    Dim lBitmap As Long
	
	    tSI.GdiplusVersion = 1
	    lRes = GdiplusStartup(lGDIP, tSI, 0)
	     
	    If lRes = 0 Then
	        lRes = GdipCreateBitmapFromHBITMAP(hBmp, 0, lBitmap)
	         
	        If lRes = 0 Then
	            Dim tJpgEncoder As GUID
	            Dim tParams As EncoderParameters
	        
	            CLSIDFromString StrPtr("{557CF401-1A04-11D3-9A73-0000F81EF32E}"), tJpgEncoder
	             
	            tParams.Count = 1
	            With tParams.Parameter
	                CLSIDFromString StrPtr("{1D5BE4B5-FA4A-452D-9CDD-5DB35105E7EB}"), .GUID
	                .NumberOfValues = 1
	                .type = 4
	                .Value = VarPtr(quality)
	            End With
	             
	            lRes = GdipSaveImageToFile(lBitmap, StrPtr(sFileName), tJpgEncoder, tParams)
	            If lRes = 0 Then
	                SavehBitmapToJPGFile = 0
	            Else
	                SavehBitmapToJPGFile = 1
	            End If
	            GdipDisposeImage lBitmap
	        End If
	         
	        GdiplusShutdown lGDIP
	    End If
	End Function


模块drawText
	
	Private Declare Function CopyImage Lib "user32" (ByVal handle As Long, ByVal un1 As Long, ByVal n1 As Long, ByVal n2 As Long, ByVal un2 As Long) As Long
	Private Declare Function GetDC Lib "user32" (ByVal hwnd As Long) As Long
	Private Declare Function ReleaseDC Lib "user32" (ByVal hwnd As Long, ByVal hdc As Long) As Long
	Private Declare Function CreateCompatibleBitmap Lib "gdi32" (ByVal hdc As Long, ByVal nWidth As Long, ByVal nHeight As Long) As Long
	Private Declare Function CreateCompatibleDC Lib "gdi32" (ByVal hdc As Long) As Long
	Private Declare Function SelectObject Lib "gdi32" (ByVal hdc As Long, ByVal hObject As Long) As Long
	Private Declare Function DeleteObject Lib "gdi32" (ByVal hObject As Long) As Long
	Private Declare Function DeleteDC Lib "gdi32" (ByVal hdc As Long) As Long
	Private Declare Function TextOut Lib "gdi32" Alias "TextOutA" (ByVal hdc As Long, ByVal x As Long, ByVal y As Long, ByVal lpString As String, ByVal nCount As Long) As Long
	Private Declare Function drawText Lib "user32" Alias "DrawTextA" (ByVal hdc As Long, ByVal lpStr As String, ByVal nCount As Long, lpRect As RECT, ByVal wFormat As Long) As Long

	设置指定DC的背景混合模式，背景混合模式用于与文本，填充画刷和当画笔不是实线时
	Private Declare Function SetBkMode Lib "gdi32" (ByVal hdc As Long, ByVal nBkMode As Long) As Long
	Private Declare Function CreateFontIndirect Lib "gdi32" Alias "CreateFontIndirectA" (lpLogFont As LOGFONT) As Long
	Private Declare Function GetDeviceCaps Lib "gdi32" (ByVal hdc As Long, ByVal nIndex As Long) As Long
	Private Declare Function SetTextColor Lib "gdi32" (ByVal hdc As Long, ByVal crColor As Long) As Long
	Private Declare Function MulDiv Lib "kernel32" (ByVal nNumber As Long, ByVal nNumerator As Long, ByVal nDenominator As Long) As Long
	
	Private Const LOGPIXELSX = 88        '  Logical pixels/inch in X
	Private Const LF_FACESIZE = 32
	Private Const TRANSPARENT = 1
	Private Const LR_COPYRETURNORG = &H4
	Private Const IMAGE_BITMAP = 0
	Private Const DEFAULT_CHARSET = 1
	Private Const DT_WORDBREAK = &H10
	
	Private Type RECT
	        Left As Long
	        Top As Long
	        Right As Long
	        Bottom As Long
	End Type
	Private Type LOGFONT
	        lfHeight As Long
	        lfWidth As Long
	        lfEscapement As Long
	        lfOrientation As Long
	        lfWeight As Long
	        lfItalic As Byte
	        lfUnderline As Byte
	        lfStrikeOut As Byte
	        lfCharSet As Byte
	        lfOutPrecision As Byte
	        lfClipPrecision As Byte
	        lfQuality As Byte
	        lfPitchAndFamily As Byte
	        lfFaceName As String * LF_FACESIZE
	End Type
	
	'DrawTextOnPicture过程
	'功能：在图片上加上指定文字，并另存为jpg文件
	'参数：
	    '1. sSourceFileName：   String类型，必要参数，源图片完整路径和文件名，可以是bmp, jpg, gif格式
	    '2. sTargetFileName：   String类型，必要参数，输出图片完整路径和文件名，jpg格式
	    '3. sText：             String类型，必要参数，在源图片上添加的文字，可以包含换行符vbCrlf用于手动换行，也可设置AutoWrap参数为True让程序自动换行
	    '4. lTextColor：        Long类型，可选参数，用于指定文字颜色，默认为黑色
	    '5. TextFont：          stdFont类型，可选参数，用于设置文字的字体名称、字号（以磅为单位）、加粗、斜体、删除线等效果，默认使用系统默认字体
	    '6. JPGquality：        Integer类型，可选参数，用于指定输出jpg图片的品质，可选值范围为1~100，数值越大，图像越清晰，默认为100
	    '7. AutoWrap：          Boolean类型，可选参数，用于指定当文本水平方向长度超出图片宽度时是否自动换行，默认为True
	    
	Sub DrawTextOnPicture(sSourceFileName As String, STargetFileName As String, sText As String, Optional lTextColor As Long = 0, Optional TextFont As stdole.StdFont, _
	                        Optional JPGquality As Integer = 100, Optional AutoWrap As Boolean = True)
	    Dim oPic As StdPicture
	    Dim hBmp As Long, hBmpPrev As Long
	    Dim hDCScreen As Long, hDCmem As Long
	    Dim hFont As Long, hFontPrev As Long
	    Dim rc As RECT
	    Dim lPixelsPerInch As Long
	    Dim lf As LOGFONT
	    
	    On Error Resume Next
	    Set oPic = LoadPicture(sSourceFileName)
	    On Error GoTo 0
	    If oPic Is Nothing Then
	        'MsgBox "不可识别的源图片格式，仅支持bmp,jpg, gif"
	        Exit Sub
	    End If
	    hBmp = CopyImage(oPic.handle, IMAGE_BITMAP, 0, 0, LR_COPYRETURNORG)
	    hDCScreen = GetDC(0)
	    lPixelsPerInch = GetDeviceCaps(hDCScreen, LOGPIXELSX)
	    hDCmem = CreateCompatibleDC(hDCScreen)
	    ReleaseDC 0, hDCScreen
	
	    hBmpPrev = SelectObject(hDCmem, hBmp)
	    hBrushPrev = SelectObject(hDCmem, hBrush)
	    rc.Right = oPic.Width * lPixelsPerInch / 2540
	    rc.Bottom = oPic.Height * lPixelsPerInch / 2540
	    SetBkMode hDCmem, TRANSPARENT
	    SetTextColor hDCmem, lTextColor
	    If Not TextFont Is Nothing Then
	       lf.lfHeight = -MulDiv(TextFont.Size, lPixelsPerInch, 72)
	       lf.lfCharSet = DEFAULT_CHARSET
	       lf.lfFaceName = TextFont.Name & Chr(0)
	       lf.lfItalic = TextFont.Italic
	       lf.lfUnderline = TextFont.Underline
	       lf.lfStrikeOut = TextFont.Strikethrough
	       lf.lfWeight = IIf(TextFont.Bold, 900, 0)
	       hFont = CreateFontIndirect(lf)
	       hFontPrev = SelectObject(hDCmem, hFont)
	       drawText hDCmem, sText & Chr(0), -1, rc, IIf(AutoWrap, DT_WORDBREAK, 0)
	       SelectObject hDCmem, hFontPrev
	       DeleteObject hFont
	    Else
	        drawText hDCmem, sText & Chr(0), -1, rc, IIf(AutoWrap, DT_WORDBREAK, 0)
	    End If
	    
	    SelectObject hDCmem, hBmpPrev
	    DeleteDC hDCmem
	    
	    Call SavehBitmapToJPGFile(hBmp, STargetFileName, JPGquality)
	    
	    DeleteObject hBmp
	End Sub
	
	Sub test() '测试代码，在test.jpg上写上文字（华文琥珀字体、字号为30磅），并另存为test(1).jpg和test(2).jpg
	    Dim oFont As stdole.StdFont
	    Dim path As String
	    Dim pic_name As String
	    Dim i As Long
	    
	    pic_name = "test"
	    path = ThisWorkbook.path & "\"
	        
	    Set oFont = New StdFont
	    With oFont
	        .Name = "宋体"
	        .Size = 80
	        .Bold = True
	    End With
	    
	    pic_name = path & "test.jpg"
	        
	    DrawTextOnPicture pic_name, pic_name, "pic_test", vbRed, oFont, , True
	    
	End Sub
