http://club.excelhome.net/forum.php?mod=viewthread&tid=1113365&extra=page%3D1

默默在这也混了许多年了，学到了许多，谢谢大家，以下一点个人心得供分享。

跟很多朋友一样，对于VBA的类一直摸不着头脑，觉得它的应用貌似不如想象中的强大，直到阅读了《设计模式》的相关书籍后，才有一点心得，分享如下，当然设计模式的思想概念什么的不会再本文中展开叙述的。看书的直接后果导致个人目前认为技术到最后，褪去华丽的技巧，剩下能闪烁的还是思想。

关于类的基础知识，这边就不赘述了，相关资料有许多。

个人在实际工作中运用最多的场景是：打开一个数据源，取出相关数据，根据不同的需求加工整理后输出。（本人做财务的）
针对上面的场景，我们来进行相关的设计，至于里面用到了哪种或哪些设计模式大家有兴趣可以自行研究：

如果强行说要去弄类的各种属性，也只不过是把数据源中的各个标题字段搞一遍而已（当然如果连数据库中的数据都得亲自设计那就另外一回事了），貌似是画蛇添足，所以我一般不搞，直接就是上类的方法。

数据源的类型包括文本文件、excel文件、各种数据库（Access，Oracle，SqlServer……）等等
很显然我们要一个Open的方法来打开数据源，这边假定数据源是excel文件，所以定义了一个方法叫OpenWorkBook()
很多情况下，数据源不只一个excel文件，而是一批（当然一批可以只包含一个），我们需要一个数组在存放数据源的名称，所以定义了一个方法叫FileArray()
更经常的情况是我们会把所有的excel表放在同一个文件夹中，这样上面FileArray()方法需要一个路径，所以定义一个方法叫FolderPath()
打开数据源取完数后，我们要关闭数据源，所以需要一个方法CloseWorkbook()
通常我还会定义一个方法用来释放程序过程中用到的对象FreeObj()
这样一个通用接口类的方法，基本上就都出来了，汇总如下：
接口类的名称为：INormal

所涉及的方法包括：

        Public Function OpenWorkbook(Optional fullfilename As Variant) As Variant
        End Function

        Public Function FileArray() As Variant
        End Function

        Public Function FolderPath() As Variant
        End Function

        Public Function CloseWorkbook() As Variant
        End Function

        Public Function FreeObj() As Variant
        End Function
以上有几点需要注意一下：
接口类的名称遵循惯例以I打头
接口中涉及的方法名称不要有下划线，据我的经验，如果接口方法名称中带下划线的话，在实现类中不容易自动生成相关的实现方法名称（这一点纯属个人经验，未经官方证实）。

上面这个接口类是打底的，还会创建另一个接口类，新建的接口类名称为IChange，具体包含以下方法：
打开数据源后，我们要取得相关的源数据，这样我们就需要一个方法GetSource()
这些源数据取得后要进行一定的加工整理，所以定义一个方法TransSource()
加工整理完以后，要进行输出，所以定义一个方法FillSource()
整理如下：

    Public Function GetSource() As Variant
    End Function
    
    Public Function TransSource(Optional arr As Variant) As Variant
    End Function
    
    Public Function FillSource(Optional arr As Variant) As Variant
    End Function

至此两个接口类已设置完成

现在来看实现类
先建一个实现类，名称为Company，引用上面的两个接口

    Implements INormal
    Implements IChange


首先实现接口类INormal的方法：
    
    Private Function INormal_OpenWorkbook(Optional fullfilename As Variant) As Variant
    End Function
    
    Private Function INormal_FolderPath() As Variant
    End Function
    
    Private Function INormal_FileArray() As Variant
    End Function
    
    Private Function INormal_CloseWorkbook() As Variant
    End Function
    
    Private Function INormal_FreeObj() As Variant
    End Function

再实现IChange接口类的方法

    Private Function IChange_GetSource() As Variant
        dim arr    dim brr()
        INormal_FolderPath
        arr = INormal_FileArray
        If IsArray(arr) Then
            For i = 0 To UBound(arr)
                INormal_OpenWorkbook arr(i)
                ……
                INormal_CloseWorkbook
            Next
            Erase arr
        End If
        IChange_GetSource = brr
    End Function

    Private Function IChange_TransSource( Optional arr As Variant ) As Variant
    End Function
    
    Private Function IChange_FillSource(Optional arr As Variant) As Variant
    End Function

更经常的我会把类的构造函数与析构函数利用起来

    Private Sub Class_Initialize()
        ……
    End Sub
    
    Private Sub Class_Terminate()
        INormal_FreeObj
        ……
    End Sub

当然上面这些实现类的中所涉及的具体方法，都需要根据具体情况自行编程，上面都只是一个框架。
这个Company类是针对一个需求搞出来的

现在我们再根据另外一个需求再新增一个实现类Salary（数据源跟前面的类一样，但加工过程、输出要求等不一样）
Salary类的相关方法与Company类似，当然具体实现细节是不一样的，但都是继承前面的两个接口，方法名称都一样。

至此我们搭积木盖房子所需要的积木基本上就搞出来了，人家说巧妇难为无米之炊，这米得先备着。

开始煮菜：热锅、放油……
新建一模块（不是类模块），命名为：Pro
声明一接口类的对象类：

    Public clsIChange As IChange

首先我们要知道程序需要哪个实现类，所以我们在该模块中定义一个函数getClass，通常我们需要一个标志来区分，这边我以当前工作表的名称来区分所用的实现类（现实工作中经常这样，在一个工作簿中，给需求方发不同的表）。
具体如下：

    Function getClass() As Variant
        Select Case ActiveSheet.Name
            Case "Comp"
                Set clsIChange = New Company
            Case "Sala"
                Set clsIChange = New Salary
        End Select
    End Function

以上是可扩展的，如果还有其他表及对应的其他实现类，只需要增加Case即可。

接着定义一个函数进行类数据的取数加工输出:

    Function getClassList() As Variant
        Dim arr, brr
        With clsIChange
            arr = .GetSource
            brr = .TransSource(arr)
            .FillSource brr
        End With
        If IsArray(arr) Then Erase arr
        If IsArray(brr) Then Erase brr
    End Function

以上我是为了调试哪个步骤出错多分了几步，调试正常或稳定的函数可以更简短如下：

    Function getClassList() As Variant
        With clsIChange
            .FillSource .TransSource(.GetSource)
        End With
    End Function


但个人不赞成这么做，因为需求不停在变，说不定哪天你又要开始调试了。当然我应该倡导有问题要输出详细的文档来处理，但单步调试通常对于我们来说是比较能掌握的实用方法。

基本上菜做完了，要出锅了……

我们定义一个main过程

    Sub main()
        getClass
        getClassList
    End Sub


最后用一个控件关联main即可，这个控件可以用加载宏搞到菜单栏中，这样在不同的工作表中点击它就可以取得不同数据结果了。
号外：我通常不是要关联控件的过程，都写为函数以Function打头，而不管它到底是否需要返回值，这是因为以前有过经历，当要关联控件宏时，如果都是Sub，会出来一堆的列表，不容易选。

至此已经是讲完了，可能大多数的朋友还没体会出它的好用之处，简单提示几点：

编程最烦的是什么？需求的变更！——一个笑话是要折磨一个程序员最好的方法是：不断的变更需求。
需求变更后，我们需要调整代码，但调整的代码是否会影响其他未调整的部分？是否会引进新的bug？这些我们都不敢保证。

但以上基于设计模式的框架在很大程度上避免了这些问题，比如现在Comp表的数据加工需求变了，这时我们只要调整Company类的TransSource方法即可，而不用担心它会影响到其他。