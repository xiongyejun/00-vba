[http://club.excelhome.net/thread-1159783-38-1.html](http://club.excelhome.net/thread-1159783-38-1.html)

# 上传文件 #
同样，上传文件也可以用fiddler抓包。

以EH的论坛上传附件为例。我们上传一个很小的压缩文件来测试（太大了的话，Fiddler的Raw里看不完整）。

在fiddler里搜索文件名（不含路径），确认我们需要模拟的网页。

![](http://files.c.excelhome.net/forum/201411/01/165058h87ehye9zcceyzh8.png)

webforms更清晰：

![](http://files.c.excelhome.net/forum/201411/01/1650598kccgqauvlvw7w64.png)

我们看到上传文件的POST的特点：

- 1、Content-Type是multipart/form-data，而且后面有一个boundary的赋值（10个英文减号+30个随机字母数字混合字符串），普通的POST的Content-Type是application/x-www-form-urlencoded了。
- 2、SendData的内容，各项内容以Content-Type里的boundary值分隔，但前面还加了两个英文减号，最后的boundary在末尾处也加了两个英文减号。
- 3、每项内容除文件流外是“Content-Disposition: form-data; name="名称"”+空行+值组成。
- 4、文件转成了二进制流，作为SendData其中一项内容，Content比其余各项略微复杂些。

我们也只需要按照这样的格式进行POST发送。

但要注意：字符串与字符串可以用“&”连接，但字符串与二进制流不能将流转为文本后用“&”连接，而应将字符串转为二进制流后再进行连接（数组方式、adostream均可连接流数据）。二进制流转为文本后会被系统按照默认编码格式进行编码，这样容易导致上传文件的格式不正确。

EH上传附件的代码（手工填入hash码即可上传了）：

	Sub Main()
	    Const Uid As String = "" '论坛UID
	    Const Hash As String = "" '上传的Hash，从Fiddler里取
	    Dim Boundary As String
	    Dim SendData
	    Dim FileFullName As String
	    Dim FileShortName As String
	    Dim Title As String
	    Dim Filetype As String
	    
	    FileFullName = "D:\测试2.rar"
	    FileShortName = Mid(FileFullName, InStrRev(FileFullName, "\") + 1)
	    Title = Left(FileShortName, InStrRev(FileShortName, ".") - 1)
	    Filetype = "rar"
	    
	    '获取Boundary
	    Boundary = GetBoundary()
	    '获取上传所需的SendData
	    SendData = GetUpLoadSendData(Boundary, FileFullName, _
	                "Filename", FileShortName, _
	                "proid", "0", _
	                "hash", Hash, _
	                "uid", Uid, _
	                "title", Title, _
	                "filetype", Filetype, _
	                "Filedata", FileShortName, _
	                "Upload", "Submit Query")
	        
	    '上传
	    With CreateObject("MSXML2.XMLHTTP")
	        .Open "POST", "http://club.excelhome.net/misc.php?mod=swfupload&fid=2&action=swfupload&operation=upload", False
	        .setRequestHeader "Content-Type", "multipart/form-data; boundary=" & Boundary
	        .Send SendData
	        Debug.Print .responsetext '出现一串数字则为成功。到论坛发帖的界面可看到“未使用的附件”的提示。
	    End With
	End Sub
	
	Function GetBoundary() As String
	    '生成Boundary
	    Dim i As Integer, r As Integer
	    Do While i < 30
	        r = Int(Rnd * 75 + 48)
	        If r < 58 Or (r > 64 And r < 91) Or r > 96 Then
	            GetBoundary = GetBoundary & Chr(r)
	            i = i + 1
	        End If
	    Loop
	    GetBoundary = String(10, "-") & GetBoundary
	End Function
	
	Function GetUpLoadSendData(Boundary As String, FileFullName As String, ParamArray NameValue()) As Byte()
	    'NameValue()必须成双，前一个是名称，后一个是值
	    'NameValue()最后一对是文件流之后的名称值对
	    'NameValue()倒数第二对是文件流信息相关的两个数据
	    
	    Dim DataBefore, DataAfter
	    Dim arrBytData(1 To 3), bytData() As Byte
	    Dim i As Long, j As Long, n As Long
	    
	    '连接文件流之前的各项名称值对
	    For i = 0 To UBound(NameValue) - 4 Step 2 '最后两对单独处理
	        DataBefore = DataBefore & "--" & Boundary & vbCrLf
	        DataBefore = DataBefore & "Content-Disposition: form-data; name=""" & NameValue(i) & """" & vbCrLf
	        DataBefore = DataBefore & vbCrLf
	        DataBefore = DataBefore & NameValue(i + 1) & vbCrLf
	    Next
	    
	    '连接文件流此项的Content-Disposition
	    DataBefore = DataBefore & "--" & Boundary & vbCrLf
	    DataBefore = DataBefore & "Content-Disposition: form-data; name=""" & NameValue(i) & """; filename=""" & NameValue(i + 1) & """" & vbCrLf
	    DataBefore = DataBefore & "Content-Type: application/octet-stream" & vbCrLf
	    DataBefore = DataBefore & vbCrLf
	    
	    '文件流前面的字符串转为流
	    arrBytData(1) = StrToUTF8Byte(DataBefore)
	    
	    '文件转流
	    arrBytData(2) = FileToByte(FileFullName)
	    
	    '文件流之后的字符串（一项）
	    DataAfter = "--" & Boundary & vbCrLf
	    DataAfter = DataAfter & "Content-Disposition: form-data; name=""" & NameValue(i) & """" & vbCrLf
	    DataAfter = DataAfter & vbCrLf
	    DataAfter = DataAfter & NameValue(i + 1) & vbCrLf
	    DataAfter = DataAfter & "--" & Boundary & "--"
	    arrBytData(3) = StrToUTF8Byte(DataAfter) '转为流
	    
	    '合并字符流和文件流
	    ReDim bytData(UBound(arrBytData(1)) + UBound(arrBytData(2)) + UBound(arrBytData(3)) + 2)
	    For i = 1 To 3
	        For j = 0 To UBound(arrBytData(i))
	            bytData(n) = arrBytData(i)(j)
	            n = n + 1
	        Next
	    Next
	    
	    GetUpLoadSendData = bytData
	End Function
	
	Function StrToUTF8Byte(strText)
	    '文本转UTF-8编码并去除BOM头
	    With CreateObject("adodb.stream")
	        .Mode = 3 'adModeReadWrite
	        .Type = 2 'adTypeText
	        .Charset = "UTF-8"
	        .Open
	        .Writetext strText
	        .Position = 0
	        .Type = 1 'adTypeBinary
	        .Position = 3 '去除UTF-8编码文本前面的BOM头（三个字节）
	        StrToUTF8Byte = .Read()
	        .Close
	    End With
	End Function
	
	Function FileToByte(strFileName As String)
	    '文件转流
	     With CreateObject("Adodb.Stream")
	        .Open
	        .Type = 1 'adTypeBinary
	        .LoadFromFile strFileName
	        FileToByte = .Read
	        .Close
	    End With
	End Function

yeah ,大功告成

分享下VBS大神Demon的上传文件的代码：

http://demon.tw/programming/vbs-post-file.html


小贴士：
文件流前后的文本如果不用UTF-8编码的话，文件可以上传成功，但文件名里的中文可能无法正确显示。