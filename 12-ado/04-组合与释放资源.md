    [enter link description here](http://club.excelhome.net/thread-521344-1-1.html)

#知识点1：sql 语句实现排列组合，学自彭版的帖子。#

#知识点2：改变excel的读写权限来避免ado操作excel自身文件而不能自动释放系统资源，学自ldy版的帖子。#

代码目的为实现0-9数字，取5个进行不重复排列组合。


	Sub bbb()
	    Dim i&, ii&, y&, inum&, lnm&, isql$, itmp1$, itmp2$
	    Dim arr1(), icnn As Object
	    
	    Application.ScreenUpdating = False
	    Application.DisplayAlerts = False
	    ThisWorkbook.ChangeFileAccess xlReadOnly
	    inum = 11
	    lnm = 5
	    For i = 1 To lnm
	        If itmp1 = "" Then
	            itmp1 = "[Sheet1$A1:A" & inum & "] as A" & i
	        Else
	            itmp1 = itmp1 & "," & "[Sheet1$A1:A" & inum & "] as A" & i
	        End If
	    Next
	
	    For i = 1 To lnm
	        For ii = i + 1 To lnm
	            ReDim Preserve arr1(y)
	            arr1(y) = "A" & i & ".数据<>A" & ii & ".数据"
	            y = y + 1
	        Next
	    Next
	    itmp2 = Join(arr1, " and ")
	    isql = "select * from " & itmp1 & " where " & itmp2
	    Set icnn = CreateObject("adodb.connection")
	    icnn.Open "provider=microsoft.jet.oledb.4.0;extended properties=excel 8.0;data source=" & ThisWorkbook.FullName
	    Sheets(2).UsedRange.Clear
	    Sheets(2).[a1].CopyFromRecordset icnn.Execute(isql)
	    
	    icnn.Close
	    Set icnn = Nothing
	    
	    ThisWorkbook.ChangeFileAccess xlReadWrite
	    Application.DisplayAlerts = True
	    Application.ScreenUpdating = True
	End Sub


20个球10黑10白放入20个盒，每盒一个。

	Sub bbb()
	    Dim i&, lnm&, isql$, itmp1$, itmp2$
	    Dim arr1(), icnn As Object
	    
	    Application.ScreenUpdating = False
	    Application.DisplayAlerts = False
	    ThisWorkbook.ChangeFileAccess xlReadOnly
	
	    lnm = 20
	    For i = 1 To lnm
	        If itmp1 = "" Then
	            itmp1 = "[Sheet1$A1:A3] as A" & i
	        Else
	            itmp1 = itmp1 & "," & "[Sheet1$A1:A3] as A" & i
	        End If
	        ReDim Preserve arr1(1 To i)
	        arr1(i) = "A" & i & ".数据"
	    Next
	
	    itmp2 = Join(arr1, "+") & "=10"
	    isql = "select * from " & itmp1 & " where " & itmp2
	    Set icnn = CreateObject("adodb.connection")
	    icnn.Open "provider=microsoft.ace.oledb.12.0;extended properties=excel 12.0;data source=" & ThisWorkbook.FullName
	    Sheets(2).UsedRange.Clear
	    Sheets(2).[a1].CopyFromRecordset icnn.Execute(isql)
	    
	    icnn.Close
	    Set icnn = Nothing
	    
	    ThisWorkbook.ChangeFileAccess xlReadWrite
	    Application.DisplayAlerts = True
	    Application.ScreenUpdating = True
	End Sub