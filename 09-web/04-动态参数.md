[http://club.excelhome.net/thread-1159783-6-1.html](http://club.excelhome.net/thread-1159783-6-1.html)

# 获取数据-防盗链的处理-动态参数 #

相同方式操作网页，抓包能得到相同的参数值，这样的参数是静态的。反之就是动态参数。

举例：（14楼作业2）
网站：http://www.caac.gov.cn/S1/GNCX/
操作：点击“查询”，获取航班信息数据。

数据网页的参数列表：

![](http://files.c.excelhome.net/forum/201410/22/164522cmzilaj3lkm4tluo.png)

sn这个参数的值是一堆数字字母混合物，像这样的参数，一般都是作为服务器辨识身份的标识。

搜索该值，查找最早出现该值的网页，先发送该网页的请求，获取sn值，然后写入目标数据网页的参数里。

最终代码如下：

	Sub Main()
	    Dim strText As String
	    Dim strHost As String
	    Dim strURL As String
	    strHost = "http://webflight.linkosky.com"
	    With CreateObject("WinHttp.WinHttpRequest.5.1")
	        
	        .Open "GET", strHost & "/WEB/Flight/WaitingSearch.aspx?JT=1&OC=PEK&DC=SHA&dstDesp=GUANGZHOU%B9%E3%D6%DD&dst2=CAN&DD=2014-10-22&DT=7&BD=&BT=7&AL=ALL&DR=true&image.x=37&image.y=14", False
	        .setRequestHeader "Referer", "http://www.caac.gov.cn/S1/GNCX/"
	        .Send
	        strText = .responsetext
	        strURL = Split(Split(strText, "setTimeout(""window.location.replace('")(1), "'")(0)
	        
	        .Open "GET", strHost & strURL, False
	        .Send
	        strText = .responsetext
	        Debug.Print strText
	        
	    End With
	End Sub

这样获取动态参数的方式，与34楼的Cookie的模拟是不是有点类似？

注：这个sn值是由查询的各种条件如日期、机场等由服务器综合计算得来的。在不知道它的算法的情况下，需要先获取先前页面获得sn值后再进行查询。

## 再上一个动态参数的例子： ##

网站：http://www.abchina.com/cn/Public ... t20101217_45743.htm
操作：币种选择“日元(JPY)”，日期选择从2014年10月1日到今天，点击查询，然后查看第6页的数据。

抓包分析步骤：
1、操作网页并抓包，复制数据。
复制数字不要包括末尾的0。因为这些0有可能是某种格式的效果，并非是数据的一部分。把0一起复制进去有可能搜索不到网页。

![](http://files.c.excelhome.net/forum/201410/22/234418w5nxn1kr1rxw161k.png)

2、搜索该数字，确认数据网页

![](http://files.c.excelhome.net/forum/201410/22/234418p52p1o64zg1ctg42.png)

3、查看Request框中Raw的内容，SendData里有参数，并且有被编码的字符：
不能用被编码的字符进行搜索

![](http://files.c.excelhome.net/forum/201410/22/234418if4q8zdzn533455y.png)

Request框中WebForms里可以看到参数的字符原型：
代表页码的参数是“__EVENTARGUMENT”。

![](http://files.c.excelhome.net/forum/201410/22/23441847hliwzu3z32q2dg.png)

4、参数太长了，fiddler的搜索框里放不下，只能截取部分字符进行搜索：
搜索到后不要忘了对整段字符串进行校对。

![](http://files.c.excelhome.net/forum/201410/22/234419bvz9ll8p89sbj9lb.png)

5、第2步骤里确认的数据网页的动态参数的来源已确认，但此网页本身也有动态参数：

![](http://files.c.excelhome.net/forum/201410/22/234419pbdb4en2dzvrpr4h.png)

6、搜索第二个动态参数。

7、找到最初源头了。这个网页没有参数了。好了，代码就从这里开始吧！

![](http://files.c.excelhome.net/forum/201410/22/234417rz50l59ts8050lsy.png)

成型代码：

	Sub Main()
	    Dim strUrl As String
	    Dim strText As String
	    Dim VIEWSTATE As String
	    Dim EVENTVALIDATION As String
	    Dim strDdr1 As String
	    Dim strDatepicker1 As String
	    Dim strDatepicker2 As String
	    Dim intPageNum As Integer
	    
	    strUrl = "http://app.abchina.com/rateinfo/RateHistorySearch.aspx"
	    strDdr1 = "日元(JPY)" '币种
	    strDatepicker1 = "2014-10-01"
	    strDatepicker2 = "2014-10-22"
	    intPageNum = 6 '页码
	    
	    With CreateObject("MSXML2.XMLHTTP")
	        '第一次GET，获取VIEWSTATE和EVENTVALIDATION
	        .Open "GET", strUrl, False
	        .send
	        strText = .responsetext
	        VIEWSTATE = encodeURI(CStr(Split(Split(strText, "__VIEWSTATE"" value=""")(1), """ />")(0)))
	        EVENTVALIDATION = encodeURI(CStr(Split(Split(strText, "__EVENTVALIDATION"" value=""")(1), """ />")(0)))
	        
	        '第二次POST，获取指定日期的VIEWSTATE和EVENTVALIDATION
	        .Open "POST", strUrl, False
	        .setrequestheader "Content-Type", "application/x-www-form-urlencoded"
	        .send "__VIEWSTATE=" & VIEWSTATE _
	            & "&__EVENTVALIDATION=" & EVENTVALIDATION _
	            & "&ddr1=" & encodeURI(strDdr1) _
	            & "&datepicker1=" & strDatepicker1 _
	            & "&datepicker2=" & strDatepicker2 _
	            & "&btnSearch=" & encodeURI("搜索")
	        strText = .responsetext
	        VIEWSTATE = encodeURI(CStr(Split(Split(strText, "__VIEWSTATE"" value=""")(1), """ />")(0)))
	        EVENTVALIDATION = encodeURI(CStr(Split(Split(strText, "__EVENTVALIDATION"" value=""")(1), """ />")(0)))
	        
	        '第三次POST，翻页
	        .Open "POST", strUrl, False
	        .setrequestheader "Content-Type", "application/x-www-form-urlencoded"
	        .send "__VIEWSTATE=" & VIEWSTATE _
	            & "&__EVENTTARGET=PagerControl1" _
	            & "&__EVENTARGUMENT=" & intPageNum _
	            & "&__EVENTVALIDATION=" & EVENTVALIDATION _
	            & "&ddr1=" & encodeURI(strDdr1) _
	            & "&datepicker1=" & strDatepicker1 _
	            & "&datepicker2=" & strDatepicker2 _
	            & "&PagerControl1_input=1"
	        strText = .responsetext
	        Debug.Print strText
	    End With
	End Sub


小贴士：aspx网页的数据查询大多有类似VIEWSTATE的参数，这个参数特点是：文本超长，且一层层传递。每层的VIEWSTATE值还会有一部分内容是相同的。所以搜索到参数值后一定要校对整个字符串是否完全一致。