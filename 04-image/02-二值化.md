[http://club.excelhome.net/forum.php?mod=viewthread&tid=1222948&extra=page%3D1](http://club.excelhome.net/forum.php?mod=viewthread&tid=1222948&extra=page%3D1)

图像的二值化，就是将图像上的像素点的灰度值设置为0或255，也就是将整个图像呈现出明显的只有黑和白的视觉效果。

一幅图像包括目标物体、背景以及噪声，要想从多值二值化的数字图像中直接提取出目标物体，最常用的方法就是设定一个阈值T，用T将图像的数据分成两部分：大于T的像素群和小于T的像素群。
这是研究灰度变换的最特殊的方法，称为图像的二值化（BINARIZATION）


根据阈值选取的不同，二值化的算法分为固定阈值和自适应阈值两种方式。 
比较常用的二值化方法则有：

- 双峰法
- P参数法
- 迭代法
- OTSU法（即大律法）
- Kittler法

本帖将就双峰法、迭代法、大律法和kittler法等，进行举例。

## 数据准备 ##

图像的二值化过程，是对灰度图像进行二值化，因此首先要将真彩--色图像像素转换为灰度图。

	Private Sub getGreyScale(Col() As Byte)     '真彩---色转换为灰度色
	    Dim I As Long, J As Long
	    Dim R As Long, G As Long, B As Long
	    ReDim lGreyLvl(lWid - 1, lHei - 1)
	    For I = 0 To lHei - 1           '加权平均法灰度转换
	        For J = 0 To lWid - 1
	            R = Col(J * 3 + 2, I)
	            G = Col(J * 3 + 1, I)
	            B = Col(J * 3, I)
	            lGreyLvl(J, I) = (R * 77 + G * 150 + B * 29) \ 256
	        Next
	    Next
	End Sub

lWid 为图片的宽度

lHei 为图片的高度

数组 col(BytesPerLine-1,lHei-1) 由BMP图片文件读入

BytesPerLine 为图片的宽度字节数，为适应数据存储的方式，字节数必须字为4的倍数，因此：
BytesPerLine =  int ((lWid * 3 +3) / 4) * 4

数组 lGreyLvl(J, I) 为使用加权平均法计算求得的灰度值

## 生成灰度直方图 ##

生成灰度直方图，就是统计图片的灰度值的分布。

因为24位真彩 * 色只有256级灰度，即灰度值在 0~255 之间

获取灰度直方图的代码如下：

	Private Sub GetHistogram()      '获取直方图
	    Dim E As Long, F As Long
	    For E = 0 To 255            '直方图初始化
	        lHistogram(E) = 0
	    Next
	    For E = 0 To lWid - 1
	        For F = 0 To lHei - 1
	            lHistogram(lGreyLvl(E, F)) = lHistogram(lGreyLvl(E, F)) + 1
	        Next
	    Next
	End Sub

迭代法获取最佳阈值

首先假定一个阈值 T0

T0为最大灰度值和最小灰度值的平均值

即，分别求出最大灰度值 iMax 和最小灰度值 iMin

	T0=(iMax + iMin) \ 2

第二步，根据 T0 将灰度图片分为前景色和背景色，分别求出前景色平均灰度值AvgFore和背景色的平均灰度值AvgBack

	AvgBack = (∑( i = iMin to T0) i * Histogram(i)) / (∑( i = iMin to T0)  Histogram(i))


	AvgFore = (∑( i =(T0 + 1) to iMax) i * Histogram(i)) / (∑( i = (T0 + 1) to iMax)  Histogram(i))

然后求得迭代阈值 T1，并进行判断是否为目标阈值。

	T1 = (AvgBack + AvgFore) \ 2

如果 T1 = T0 ,则 T1 即为目标阈值；

否则，T0 = T1，继续从第二步进行迭代取值，直至 T1 = T0 为止。

完整代码如下：
	
	Private Function Iteration() As Boolean     '迭代法获取最佳阈值
	    Dim A As Long, B As Long, C As Long
	    Dim iMin As Integer, iMax As Integer
	    Dim iItThreshold As Integer, bError As Byte
	    Dim lIntegralBack As Long, lIntegralFore As Long
	    Dim lSumBack As Long, lSumFore As Long
	    Dim dAvgBack As Double, dAvgFore As Double
	    For A = 0 To lWid - 1
	        For B = 0 To lHei - 1
	            If iMin > lGreyLvl(A, B) Then
	                iMin = lGreyLvl(A, B)
	            End If
	            If iMax < lGreyLvl(A, B) Then
	                iMax = lGreyLvl(A, B)
	            End If
	        Next
	    Next
	    bError = 1      '设定误差范围
	    C = 0
	    iThreshold = 0
	    iItThreshold = (iMax + iMin) \ 2        '设定初始迭代阈值为最大灰度值和最小灰度值的均值
	    Do While Math.Abs(iThreshold - iItThreshold) > bError
	        lSumBack = 0
	        lSumFore = 0
	        lIntegralBack = 0
	        lIntegralFore = 0
	        For A = iMin To iItThreshold                '背景色阈值
	            lSumBack = lSumBack + lHistogram(A)
	            lIntegralBack = lIntegralBack + lHistogram(A) * A
	        Next
	        For B = iItThreshold + 1 To iMax            '前景色阈值
	            lSumFore = lSumFore + lHistogram(B)
	            lIntegralFore = lIntegralFore + lHistogram(B) * B
	        Next
	        dAvgBack = IIf(lSumBack <> 0, lIntegralBack / lSumBack, 0)
	        dAvgFore = IIf(lSumFore <> 0, lIntegralFore / lSumFore, 0)
	        iThreshold = iItThreshold
	        iItThreshold = (dAvgBack + dAvgFore) \ 2        '渐进逼近目标阈值
	        C = C + 1
	        If C > 999 Then
	            Iteration = False
	            Exit Function
	        End If
	    Loop
	    Iteration = True
	End Function


大律法二值化大津法由由日本学者大津(Nobuyuki Otsu)于1979年提出的。
对于图像，假设 T 为前景与背景的分割阈值，假设前景点数占图像比例为w0，平均灰度为 u0；背景点数占比为w1，平均灰度为u1。
因此，图像的总平均灰度为：

	u = w0 * u0 + w1 * u1

从最小灰度值到最大灰度值遍历 T 阈值，当 T 使得值 g=w0 * (u0 - u)^2+w1 * (u1 - u) ^ 2 最大时，T即为分割的最佳阈值。

该式实际上是类间方差值，因为方差是灰度分布均匀性（或偏离程度）的一种度量。

方差值越大，说明构成图像的前景和背景两部分差别越大， 当部分前景错分为背景或部分背景错分为前景时都会导致两部分差别变小，因此使类间方差最大的分割意味着错分概率最小。

直接应用大津法计算量是较大，因此通常采用其等价的公式

	g = w0 * w1 * (u0 - u1)^2

代码如下：

	Private Sub OtsuBinarization()      '大律法二值化
	    Dim lTotal As Long, lSum As Long
	    Dim lTotalBack As Double, lSumBack As Double
	    Dim du As Double, dv As Double
	    Dim dOstu As Double, dMaxOstu As Double
	    Dim A As Long, B As Long
	    iThreshold = 0
	    For A = 0 To 255
	        lTotal = lTotal + lHistogram(A)
	        lSum = lSum + lHistogram(A) * A
	    Next
	    For A = 0 To 255
	        lTotalBack = 0
	        lSumBack = 0
	        For B = 0 To A
	            lTotalBack = lTotalBack + lHistogram(B)
	            lSumBack = lSumBack + lHistogram(B) * B
	        Next
	        du = IIf(lTotalBack > 0, lSumBack / lTotalBack, 0)
	        If lTotal - lTotalBack > 0 Then
	            dv = (lSum - lSumBack) / (lTotal - lTotalBack)
	        Else
	            dv = 0
	        End If
	        dOstu = lTotalBack * (lTotal - lTotalBack) * (du - dv) * (du - dv)
	        If dOstu > dMaxOstu Then
	            dMaxOstu = dOstu
	            iThreshold = A
	        End If
	    Next
	End Sub

iThreshold 即为最终阈值。


## 双峰法二值化 ##

双峰法的原理极其简单，它认为图像由前景和背景组成，在灰度直方图上，前后二景都形成高峰，在双峰之间的最低谷处就是图像的阈值所在。

双峰法在应对前景和背景对比强烈时效果较好，否则基本无效。

为改善这一特点，本帖对灰度直方图使用累积法进行平滑化，然后迭代求阈，效果有所改善。

	Private Function PeakValley() As Boolean     '双峰法迭代获取最佳阈值
	    Dim A As Long, C As Long
	    Dim dSubHisto(255) As Double, dConHisto(255) As Double
	    Dim isPeak As Boolean
	    
	    For A = 0 To 255
	        dSubHisto(A) = lHistogram(A)
	        dConHisto(A) = lHistogram(A)
	    Next
	    Do While Not HasTwoPeaks(dConHisto)
	        dConHisto(0) = (dSubHisto(0) * 2 + dSubHisto(1)) / 3        '灰度直方图连续三个灰度值进行平滑化
	        For A = 1 To 255 - 1
	            dConHisto(A) = (dSubHisto(A - 1) + dSubHisto(A) + dSubHisto(A + 1)) / 3
	        Next
	        dConHisto(A) = (dSubHisto(A - 1) + dSubHisto(A) * 2) / 3
	        For A = 0 To 255                                            '备份平滑化后的直方图，以备再次迭代
	            dSubHisto(A) = dConHisto(A)
	        Next
	        C = C + 1
	        If C > 999 Then
	            PeakValley = False
	            Exit Function
	        End If
	    Loop
	    isPeak = False
	    For A = 1 To 255 - 1
	        If (dConHisto(A - 1) < dConHisto(A)) And (dConHisto(A + 1) < dConHisto(A)) Then     '判断首峰
	            isPeak = True
	        End If
	        If isPeak And (dConHisto(A - 1) >= dConHisto(A)) And (dConHisto(A + 1) >= dConHisto(A)) Then        '二峰
	            PeakValley = True
	            iThreshold = A - 1
	            Exit Function
	        End If
	    Next
	    PeakValley = False
	End Function
	
	Private Function HasTwoPeaks(Histo() As Double) As Boolean      '是否存在双峰，超过2个或只有1个峰，都返回否定
	    Dim A As Integer
	    Dim PeakNum As Byte
	    For A = 1 To 255 - 1
	        If (Histo(A - 1) < Histo(A)) And (Histo(A + 1) < Histo(A)) Then
	            PeakNum = PeakNum + 1
	            If PeakNum > 2 Then
	                HasTwoPeaks = False
	                Exit Function
	            End If
	        End If
	    Next
	    HasTwoPeaks = (PeakNum = 2)
	End Function

