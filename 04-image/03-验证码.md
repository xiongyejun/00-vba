[http://club.excelhome.net/forum.php?mod=viewthread&tid=896161&extra=page%3D1](http://club.excelhome.net/forum.php?mod=viewthread&tid=896161&extra=page%3D1)

	'api引用
	Public Declare Function GetClipboardData Lib "user32" (ByVal wFormat As Long) As Long    '获取剪贴板内容
	Public Declare Function CloseClipboard Lib "user32" () As Long                     '关闭剪贴板
	Public Declare Function OpenClipboard Lib "user32" (ByVal hwnd As Long) As Long    '打开剪贴板
	Public Declare Function GlobalLock Lib "kernel32" (ByVal hMem As Long) As Long     '锁定全局内存对象中指定的内存块，并返回一个地址值，令其指向内存块的起始处
	Public Declare Function GlobalSize Lib "kernel32" (ByVal hMem As Long) As Long     '取得剪贴板数据大小
	Public Declare Function GlobalUnlock Lib "kernel32" (ByVal hMem As Long) As Long   '解除被锁定的全局内存对象
	Public Declare Sub CopyMemory Lib "kernel32" Alias "RtlMoveMemory" (Destination As Any, Source As Any, ByVal Length As Long)    '将一块内存的数据从一个位置复制到另一个位置
	Public Declare Function EmptyClipboard Lib "user32" () As Long                     '清空剪贴板并释放剪贴板内数据的句柄。
	'剪贴版数据格式定义
	Public Const CF_TEXT = 1
	Public Const CF_BITMAP = 2
	Public Const CF_DIB = 8    '位图，这是我们下面要用到的
	'DIB的结构
	'Private Type BITMAPFILEHEADER    'BMP文件头——BITMAPFILEHEADER
	'    bfType(0 To 1) As Byte       'bfType 指示文件的类型，必须是“BM”
	'    bfSize As Long               'bfSize# 指示文件的大小，包括BITMAPFILEHEADER
	'    bfReserved1 As Integer       'bfReserved1 保留，=0
	'    bfReserved2 As Integer       'bfReserved2 保留，=0
	'    bfOffBits As Long            'bfOffBits# 从文件头到位图数据的偏移字节数
	'End Type
	Private Type BITMAPINFOHEADER   '文件信息头——BITMAPINFOHEADER
	    biSize As Long              'biSize BITMAPINFOHEADER结构的大小。BMP有多个版本，就靠biSize来区别：BMP3.0：BITMAPINFOHEADER（=40）,BMP4.0：BITMAPV4HEADER（=108）,BMP5.0：BITMAPV5HEADER（=124）
	    biWidth As Long             'biWidth 位图的宽度，单位是像素
	    biHeight As Long            'biHeight 位图的高度，单位是像素
	    biPlanes As Integer         'biPlanes 设备的位平面数。现在都是1
	    biBitCount As Integer       'biBitCount 图像的颜色位数:0：当biCompression=BI_JPEG时必须为0（BMP 5.0）, 1：单色位图,4：16色位图,8：256色位图,16：增强色位图，默认为555格式,24：真彩色位图,32：32位位图，默认情况下Windows不会处理最高8位，可以将它作为自己的Alpha通道
	    biCompression As Long       'biCompression 压缩方式:BI_RGB：无压缩,BI_RLE8：行程编码压缩，biBitCount必须等于8,BI_RLE4：行程编码压缩，biBitCount必须等于4,BI_BITFIELDS：指定RGB掩码，biBitCount必须等于16、32,BI_JPEG：JPEG压缩（BMP 5.0）,BI_PNG：PNG压缩（BMP 5.0）
	    biSizeImage As Long         'biSizeImage# 实际的位图数据所占字节（biCompression=BI_RGB时可以省略）
	    biXPelsPerMeter As Long     'biXPelsPerMeter# 目标设备的水平分辨率，单位是每米的像素个数
	    biYPelsPerMeter As Long     'biYPelsPerMeter# 目标设备的垂直分辨率，单位是每米的像素个数
	    biClrUsed As Long           'biClrUsed# 使用的颜色数（当biBitCount等于1、4、8时才有效）。如果该项为0，表示颜色数为2^biBitCount
	    biClrImportant As Long      'biClrImportant# 重要的颜色数。如果该项为0，表示所有颜色都是重要的
	End Type
	Private Type RGBQUAD     '调色板,只有biBitCount等于1、4、8时才有调色板。调色板实际上是一个数组，元素的个数由biBitCount和biClrUsed决定。
	    rgbBlue As Byte      'rgbBlue 蓝色分量
	    rgbGreen As Byte     'rgbGreen 绿色分量
	    rgbRed As Byte       'rgbRed 红色分量
	    rgbReserved As Byte  'rgbReserved# 保留，=0
	End Type
	Private Type bitmapinfo  'bitmapinfoheader结构和调色板数据合在一起就构成了bitmapinfo结构，这个结构在显示位图文件时能够用到
	    bmiheader As BITMAPINFOHEADER
	    bmicolors(0 To 255) As RGBQUAD
	End Type
	Sub 验证码识别()
	    Dim img          '定义目标图片对象
	    Dim CtrlRange    '定义非文本对象
	    Dim bytClipData() As Byte        '定义数组（一维）
	    Dim arr() As String              '定义数组（一维）
	    Dim brr()                        '定义二值化数组
	    Dim ts As Integer                '定义整数
	    Dim wjxxt As BITMAPINFOHEADER    '定义文件信息头——BITMAPINFOHEADER
	    Dim tsb As RGBQUAD               '定义调色板
	    Dim xt As bitmapinfo             '定义bitmapinfo结构
	    Cells.ClearContents              '清空工作表
	    On Error Resume Next
	    With CreateObject("InternetExplorer.application")    '创建一个空的ie
	        .Visible = True                                  '让ie可见
	        .Navigate "http://www.zjadc.com/ADCCSSPortal/ValidateCode/generatevalidatecode.aspx"
	        Do Until .ReadyState = 4                         '等待ie完毕加载
	            DoEvents
	        Loop
	        Set img = .Document.All.tags("img")(0)                 '指定（验证码）目标图片
	        Set CtrlRange = .Document.body.createControlRange()    '创建非文本对象 ControlRange 集合
	        CtrlRange.Add img                                      '向非文本对象 ControlRange 集合中添加 img 对象
	        CtrlRange.execCommand "Copy", True                     '从 ControlRange 集合中copy img 对象（图片）到剪贴板,这样子读取的图片是不包含“位图文件头”的。也就是说，是从位图文件的第二部分开始读取的
	        Dim hMem As Long, lpData As Long
	        OpenClipboard 0&                     '打开剪贴板
	        hMem = GetClipboardData(8)           '获得剪贴板数据，指定格式为：CF_DIB = 8
	        If CBool(hMem) Then                  '判断hMem是否存在，也就是说是否复制了图片
	            lpData = GlobalLock(hMem)        '锁定内存对象hMen
	            lClipSize = GlobalSize(hMem)     '获得剪贴板数据字节数
	            If lpData <> 0 And lClipSize > 0 Then
	                ReDim bytClipData(0 To lClipSize - 1)                 '重新定义字节数组大小
	                CopyMemory bytClipData(0), ByVal lpData, lClipSize    '把剪贴板数据转移到字节数组
	                CopyMemory wjxxt, ByVal lpData, bytClipData(0)        '把剪贴板数据转移到文件信息头——BITMAPINFOHEADER的wjxxt数组
	                With wjxxt
	                    tsbcd = lClipSize - .biSizeImage - .biSize        '调色板长度,tsbcd=0则无调色板
	                    txmhzjs = .biSizeImage / .biHeight                '图像每行字节数(肯定是4的倍数)
	                    txmxszjs = Int(txmhzjs / .biWidth)                '图像每像素字节数
	                    txmd0 = txmhzjs - txmxszjs * .biWidth             '图像末端填充“0”的字节数
	                    If tsbcd = 1024 Then
	                        CopyMemory xt, ByVal lpData, tsbcd + .biSize  '把剪贴板数据转移到bitmapinfo的xt数组
	                    End If
	                End With
	            End If
	            GlobalUnlock hMem    '解除锁定内存对象hMen
	        End If
	        EmptyClipboard           '使用了剪贴板后，就要记着清空它，
	        CloseClipboard           '关闭剪贴板
	        a1 = wjxxt.biSize        '把biSize赋给a1
	        If tsbcd > 0 Then        '如果有调色板
	            a1 = lClipSize - wjxxt.biSizeImage    '就从wjxxt.biSizeImage开始
	            txmxszjs = 1                          '并且一个字节表示一个点
	        End If
	        ReDim arr(1 To wjxxt.biWidth * wjxxt.biHeight)        '重新定义arr数组大小
	        ReDim brr(1 To wjxxt.biHeight, 1 To wjxxt.biWidth)    '重新定义brr数组大小
	        For i = 1 To wjxxt.biWidth * wjxxt.biHeight           '没有调色板的话就从第40个字节开始
	            arr(i) = ""                '1或空（就是没有）的设置，是图片显示方式不同，可以更改这个设置，来看看效果，不过要把下面的arr(i) = "1"一起改。
	            If tsbcd = 0 Then          '没有调色板
	                ts = 0                 '置初值
	                For j = 0 To txmxszjs - 1
	                    ts = ts + Val(bytClipData((i - 1) * txmxszjs + a1 + j))    '累加每一点的BGR值，从第lClipSize - wjxxt.biSizeImage个字节开始
	                Next j
	                ts = ts / txmxszjs     '图像的BGR的均值（不一定），有调色板的话就不是这个意思。应该说成是图片点的信息均值更贴切些，
	            Else                       '有调色板
	                ts = 0
	                ts = ts + Val(xt.bmicolors(Val(bytClipData((i - 1) * txmxszjs + a1 + j))).rgbBlue)     '从调色板取B值
	                ts = ts + Val(xt.bmicolors(Val(bytClipData((i - 1) * txmxszjs + a1 + j))).rgbGreen)    '从调色板取G值
	                ts = ts + Val(xt.bmicolors(Val(bytClipData((i - 1) * txmxszjs + a1 + j))).rgbRed)      '从调色板取R值
	                ts = ts / 3
	            End If
	            If ts > 10 Then         '如果图像的BGR的均值>10，那么就把“1”赋给数组arr(i)
	                arr(i) = "1"        '其实就是二值化
	            End If
	            If i / wjxxt.biWidth = Int(i / wjxxt.biWidth) Then a1 = a1 + txmd0         '跳过图像每行末端的附加“0”，因为biSizeImage必须是4的整倍数
	        Next i
	        For i = 1 To wjxxt.biHeight
	            For j = 1 To wjxxt.biWidth
	                brr(wjxxt.biHeight + 1 - i, j) = arr((i - 1) * wjxxt.biWidth + j)      '把一维数组arr写入二维数组brr，注意：要倒过来，从下往上写，比直接写入单元格要快些。
	            Next j
	        Next i
	        Range(Cells(1, 1), Cells(wjxxt.biHeight, wjxxt.biWidth)) = brr                 '把二维数组brr一次性写入单元格。
	        .Quit
	    End With
	    MsgBox "ok"
	End Sub


[http://club.excelhome.net/forum.php?mod=viewthread&tid=980303&extra=page%3D1](http://club.excelhome.net/forum.php?mod=viewthread&tid=980303&extra=page%3D1)

# 验证码识别 第3辑： #

	Public Const STATUS_OK = 0
	Public Const NOERROR = 0
	Public Type GUID
	    Data1 As Long
	    Data2 As Integer
	    Data3 As Integer
	    Data4(0 To 7) As Byte
	End Type
	
	Public Type GdiplusStartupInput
	    GdiplusVersion As Long
	    DebugEventCallback As Long
	    SuppressBackgroundThread As Long
	    SuppressExternalCodecs As Long
	End Type
	
	Public Declare Function GdiplusStartup Lib "GDIPlus" (token As Long, inputbuf As GdiplusStartupInput, ByVal outputbuf As Long) As Long
	Public Declare Function GdiplusShutdown Lib "GDIPlus" (ByVal token As Long) As Long
	Public Declare Function GdipSaveImageToFile Lib "GDIPlus" (ByVal Image As Long, ByVal FileName As Long, clsidEncoder As GUID, encoderParams As Any) As Long
	Public Declare Function GdipCreateBitmapFromFile Lib "GDIPlus" (ByVal FileName As Long, BITMAP As Long) As Long
	Public Declare Function GdipDisposeImage Lib "GDIPlus" (ByVal Image As Long) As Long
	Public Declare Function CLSIDFromString Lib "ole32" (ByVal Str As Long, id As GUID) As Long
	
	Public Function PNGtoBMP(ByVal SourceFilename As String, ByVal DestinationFilename As String) As Boolean
	    Dim GSI As GdiplusStartupInput
	    Dim lGDIP As Long
	    Dim lBitmap As Long
	    Dim bmpEncoder As GUID
	    GSI.GdiplusVersion = 1
	    If GdiplusStartup(lGDIP, GSI, 0) = STATUS_OK Then
	        If GdipCreateBitmapFromFile(StrPtr(SourceFilename), lBitmap) = STATUS_OK Then
	            If CLSIDFromString(StrPtr("{557CF400-1A04-11D3-9A73-0000F81EF32E}"), bmpEncoder) = NOERROR Then
	                If GdipSaveImageToFile(lBitmap, StrPtr(DestinationFilename), bmpEncoder, ByVal 0) = STATUS_OK Then
	                    PNGtoBMP = True
	                End If
	            End If
	            Call GdipDisposeImage(lBitmap)
	        End If
	        Call GdiplusShutdown(lGDIP)
	    End If
	End Function


	Private Sub CommandButton1_Click()    '广东地税验证码，png格式，转为Bmp格式，然后识别。
	    Dim crr() As Byte
	    Dim arr()
	    Dim brr()
	    Dim ret As Boolean
	    On Error Resume Next
	    Sheet10.Activate
	    Set oDoc = CreateObject("htmlfile")
	    With CreateObject("Msxml2.XMLHTTP.6.0")
	1:
	        .Open "POST", "http://www.gdltax.gov.cn/fpzx/jsp/fpzx/mhcx/yzm.do", False
	        .SetRequestHeader "Referer", "http://www.gdltax.gov.cn/fpzx/jsp/fpzx/mhcx/mh_cjdj_dzfpsjcx_index.do?siteName=gd&styleName=blue"
	        .SetRequestHeader "Connection", "Keep-Alive"
	        .send
	        urls = "http://www.gdltax.gov.cn" & Split(Split(.responsetext, "src=""")(1), """")(0)
	        .Open "GET", urls, False
	        .send
	        If "c:\2.bmp" <> "" Then Kill "c:\2.bmp"
	        If "C:\2.png" <> "" Then Kill "C:\2.png"
	        crr = .responseBody
	        Open "c:\2.png" For Binary As #1
	        For i = 0 To UBound(.responseBody)
	            Put #1, i + 1, crr(i)
	        Next i
	        Close #1
	        ret = PNGtoBMP("C:\2.png", "C:\2.bmp")
	        Erase crr()
	        .Open "GET", "c:\2.bmp", False
	        .send
	        crr = .responseBody
	        kuandu = Val(crr(18))
	        gaodu = Val(crr(22))
	        ReDim arr(1 To kuandu * gaodu)
	        ReDim brr(1 To gaodu, 1 To kuandu)
	        a1 = 54
	        For i = 1 To kuandu * gaodu
	            arr(i) = ""
	            ts = 0
	            For j = 0 To 2
	                ts = ts + Val(crr((i - 1) * 3 + a1 + j))
	            Next j
	            ts = ts / 3
	            If ts < 180 Then
	                arr(i) = 1
	            End If
	            If i / kuandu = Int(i / kuandu) Then a1 = a1 + 0
	
	        Next i
	        For i = 1 To gaodu
	            For j = 1 To kuandu
	                brr(gaodu + 1 - i, j) = arr((i - 1) * kuandu + j)
	            Next j
	        Next i
	
	        Dim b(0 To 11)
	        Dim a(0 To 11)
	        a(0) = "1": b(0) = "44cc22"
	        a(1) = "2": b(1) = "58766676"
	        a(2) = "5": b(2) = "9a666896"
	        a(3) = "6": b(3) = "58877764"
	        a(4) = "8": b(4) = "8a7667a6"
	        a(5) = "9": b(5) = "48867995"
	        a(6) = "A": b(6) = "2433554994"
	        a(7) = "C": b(7) = "58644555"
	        a(8) = "D": b(8) = "bc44445564"
	        a(9) = "F": b(9) = "cc4444441"
	        a(10) = "V": b(10) = "377526862"
	        a(11) = "X": b(11) = "4644446564"
	        p = ""
	        For i = 1 To kuandu
	            sst = 0
	            For j = 5 To 16
	                sst = sst + brr(j, i)
	            Next j
	            If sst = 10 Then sst = "a"
	            If sst = 11 Then sst = "b"
	            If sst = 12 Then sst = "c"
	            If sst = 13 Then sst = "d"
	            p = p & sst
	            p = Replace(p, "0", " ")
	        Next i
	        For i = 0 To 11
	            p = Replace(p, b(i), a(i))
	        Next i
	        p = Replace(p, " ", "")
	        yzm = Format(p, "0000")
	        If Len(yzm) <> 4 Then GoTo 1
	        n = Range("a65536").End(xlUp).Row + 1
	        Cells(n, 1) = yzm
	        ML = Cells(n, "b").Left
	        MT = Cells(n, "b").Top
	        MW = Cells(n, "b").Width
	        MH = Cells(n, "b").Height
	        ActiveSheet.Shapes.AddShape(msoShapeRectangle, ML, MT, MW, MH).Select
	        Selection.ShapeRange.Fill.UserPicture "C:\2.png"
	        Selection.ShapeRange.Line.Visible = False
	        Erase arr()
	        Erase brr()
	        Erase crr()
	        Erase b()
	        Erase a()
	    End With
	End Sub
